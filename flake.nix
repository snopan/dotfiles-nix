{
  description = "Example nix-darwin system flake";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-25.05-darwin";
    nix-darwin.url = "github:nix-darwin/nix-darwin/nix-darwin-25.05";
    nix-darwin.inputs.nixpkgs.follows = "nixpkgs";
    nur = {
      url = "github:nix-community/NUR";
    };
    nix4vscode = {
      url = "github:nix-community/nix4vscode";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    home-manager.url = "github:nix-community/home-manager";
    home-manager.inputs.nixpkgs.follows = "nixpkgs";
  };

  outputs = inputs@{ self, nix-darwin, nur, nixpkgs, nix4vscode, home-manager }:
  let
    darwinConfig = { pkgs, specialArgs, ... }: {
      imports = [./modules/darwin/default.nix];

      # Allow unfree packages
      nixpkgs.config.allowUnfree = true;

      # Necessary for using flakes on this system.
      nix.settings.experimental-features = "nix-command flakes";

      # Enable alternative shell support in nix-darwin.
      # programs.fish.enable = true;

      # Set Git commit hash for darwin-version.
      system.configurationRevision = self.rev or self.dirtyRev or null;

      # Used for backwards compatibility, please read the changelog before changing.
      # $ darwin-rebuild changelog
      system.stateVersion = 5;

      # The platform the configuration will be used on.
      nixpkgs.hostPlatform = "aarch64-darwin";
      
      # Determinate uses its own daemon to manage the Nix installation that
      # conflicts with nix-darwinâ€™s native Nix management.
      nix.enable = false;

      # Set user for home-manager
      users.users.snopan.name = "snopan";
      users.users.snopan.home = "/Users/snopan";
    };
  in
  let homeManagerConfig = {
      home-manager.extraSpecialArgs = { inherit inputs; };
      home-manager.useGlobalPkgs = true;
      home-manager.useUserPackages = true;
      home-manager.users.snopan = import ./modules/home/default.nix;
    };
  in
  {
    # Build darwin flake using:
    # $ darwin-rebuild build --flake .#snopans-MacBook-Pro
    darwinConfigurations."snopans-MacBook-Pro" = nix-darwin.lib.darwinSystem {
      specialArgs = { inherit inputs; };
      modules = [
        darwinConfig
        home-manager.darwinModules.home-manager homeManagerConfig
      ];
    };
  };
}
